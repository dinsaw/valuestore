buildscript {
	repositories {
		mavenCentral()
	}
}

plugins {
    id 'io.franzbecker.gradle-lombok' version '1.14'
    id 'idea'
    id 'eclipse'
    id 'java'
    id 'application'
}

allprojects {
    group = 'com.github.dinsaw'
    version = '0.0.1-SNAPSHOT'

    repositories {
        mavenCentral()
        jcenter()
    }

}

subprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'jacoco'


    sourceCompatibility = 1.8

    dependencies {
        compile "com.github.dinsaw:navparser:${navParserVersion}"
        compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
        compile "io.vertx:vertx-web:${vertxVersion}"
        compile "io.vertx:vertx-config:${vertxVersion}"
        compile "io.vertx:vertx-mongo-client:${vertxVersion}"
        compile "io.vertx:vertx-web-client:${vertxVersion}"
        compile "io.vertx:vertx-health-check:${vertxVersion}"
        testCompile group: 'io.vertx', name: 'vertx-unit', version: vertxVersion
        // Use JUnit test framework
        testImplementation "junit:junit:$junitVersion"
    }

    jacocoTestReport {
        reports {
            xml.enabled true
            html.enabled false
        }
    }

    check.dependsOn jacocoTestReport
}

project(':web') {
    dependencies {
        compile project(':common')
    }
}

project(':common')
project(':ingest') {
    dependencies {
        compile project(':common')
    }
}
project(':launcher') {
    dependencies {
        compile project(':ingest')
        compile project(':web')
    }

    mainClassName = "com.github.dinsaw.valuestore.App"
    task fatJar(type: Jar) {
        manifest {
            attributes(
                    'Class-Path': configurations.compile.collect { it.getName() }.join(' '),
                    'Main-Class': mainClassName
            )
        }
        classifier = 'all'
        from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
        with jar
    }
}